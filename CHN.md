# SAS简明教程

## 1 简介

### 1.1 SAS 编程语言

与许多其他软件应用程序不同，它们要么是菜单驱动，要么是命令驱动，SAS 使用声明（Statement）来编写一系列指令，称为 SAS 程序。

SAS 程序是按顺序执行的一系列声明。声明向 SAS 提供信息或指令，并必须在程序中处于适当的放置。后续语句的顺序可能并不重要，但你必须从一般性的陈述开始，说明你想要做什么，比如两个基本的构建块：`DATA` 步骤和 `PROC` 步骤。

与任何语言一样，在编写 SAS 程序时需要遵循一些规则。最重要的规则是**每个 SAS 声明都以分号结尾**。

关于如何格式化编写 SAS 程序并没有硬性规定。虽然将每个声明单独放在一行上，并使用缩进来展示程序的各个部分有助于使程序看起来整洁，但这并不是必需的。

* SAS 声明可以使用大写或小写（类似于 SQL）。
* 声明可以在换行继续编写。
* 每个声明可以与其他声明在同一行编写。（只要在两个声明中间加一个分号，就可以在一行中写多个声明）。
* 语句可以从任何一列开始（与 Python 不同）。

为了使你的程序更易理解，你可以在程序中插入注释。有两种注释语法可供选择：一种以星号（\*）开头，以分号（;）结尾。另一种以斜杠星号（/\*）开头，以星号斜杠（\*/）结尾。

```sas
* This is a comment;
/* This is also a comment*/
```

由于一些操作环境会将斜杠星号（/\*）解释为一段代码的结束，使用这种注释时要小心，不要将其放在第一列。

### 1.2 SAS 数据集

数据是任何数据集的主要组成部分。在传统的SAS术语中，数据由变量（Variables）和观测（Observation）组成。采用关系数据库的术语，SAS 数据集也被称为表（Tables），观测也被称为行，变量也被称为列。

原始数据有许多不同的形式，但 SAS 进行了简化。在 SAS 中，只有两种数据类型：数值型（Numeric）和字符型（Character）。数值变量可以进行加法和减法运算，可以有任意数量的小数位，可以是正数或负数。此外，数值变量可以包含加号（+）、减号（-）、小数点（.）或科学记数法中的 "E"。字符数据包括其他所有内容。它们可以包含数字、字母或特殊字符（如\$或!），并且可以长达32,767个字符。

如果一个变量包含字母或特殊字符，它必须是一个字符变量。然而，如果它只包含数字，那么它可能是数值变量也可能是字符变量。

有时，你的数据可能是不完整的。某些观测可能缺少特定变量的值。在这些情况下，缺失的字符数据用空格表示，而缺失的数值数据用一个点（.）表示。

在 SAS 9.1之前，SAS 数据集最多可以包含32,767个变量。从 SAS 9.1开始，SAS 数据集中变量的最大数量受计算机资源的限制，但包含32,767个以上变量的 SAS 数据集无法在较早版本的 SAS 中使用。无论你使用哪个版本的 SAS，观测的数量只受计算机处理和存储能力的限制。


当为数据中的变量以及数据集起名字时，请遵循以下简单规则：

* 名字长度必须在32个字符以下。
* 名字必须以字母或下划线（_）开头。
* 名字只能包含字母、数字或下划线（_）。
* 名字可以包含大写和小写字母。

最后一点十分重要，SAS 对大小写不敏感，因此你可以使用大写、小写或混合大小写。例如，`Height`、`HEIGHT`、`height`在 SAS 中被视为相同的变量。然而，对于变量名，需要注意 SAS 会记住每个变量名第一次出现时的大小写，并在打印结果时使用该大小写。

除了实际的数据之外，SAS数据集还包含有关数据集的信息，例如其名称、创建日期以及创建它所使用的 SAS 版本。SAS 还存储有关每个变量的信息，包括其名称、标签（如果有）、类型（数值型或字符型）、长度（或存储大小）以及在数据集中的位置。这些信息有时被称为数据集的描述部分，它使 SAS 数据集具有自我记录文档的特性。

### 1.3 DATA 步骤和 PROC 步骤

SAS 程序由两个基本构建块组成：DATA 步骤和 PROC 步骤。一个典型的程序以 DATA 步骤开始，用于创建一个 SAS 数据集，然后将数据传递给一个 PROC 步骤进行处理。

DATA 步骤和 PROC 步骤由声明构成。一个步骤可能有一个或数百个声明。大多数声明只在一种类型的步骤中起作用——在 DATA 步骤中但不在 PROC 步骤中，反之亦然。在大多数情况下，DATA 步骤用于读取和修改数据，而 PROC 步骤用于分析数据、执行功能或打印报告。


DATA 步骤以`DATA`声明开始。该关键字后面跟着一个你为 SAS 数据集取的名称。除了从外部原始数据文件读取数据外，DATA 步骤还可以包括 DO 循环、IF-THEN/ELSE 逻辑以及各种数值和字符函数。DATA 步骤还可以以几乎任何方式组合数据集。


另一方面，Procedure 步骤则以 `PROC` 声明开始，其中关键字后面跟着操作的名称（例如 `PRINT`、`SORT` 或 `MEANS`）。SAS 的 Procedure 可以执行从简单排序和打印到方差分析和3D图形等各种任务。

SAS 在遇到一个新的步骤（`DATA` 或 `PROC` 声明）; `RUN`、`QUIT`、`STOP` 或 `ABORT` 声明；或者（如果正在批处理模式下运行，）程序的结尾时，当前步骤会结束。`RUN` 声明告诉 SAS 运行该步骤之前的所有行，它是罕见的全局声明之一，不属于 DATA 或 PROC 步骤的一部分。

### 1.4 DATA 步骤的内置循环

DATA 步骤具有一个内在的结构，一个隐式的、内置的循环。DATA 步骤逐行执行，逐观测执行。逐行执行的概念相当直观且容易理解。这意味着，默认情况下，SAS 会先执行 DATA 步骤的第一行，之后执行第二行、第三行，以此类推。逐观测执行指 SAS 会取第一个观测并运行全部 DATA 步骤（如前文所述逐行进行），然后循环回来获取第二个观测。这样，SAS一次只处理一个观测。

这张图解说明了观测是如何通过一个 DATA 步骤的：

![Img](./FILES/ENG.md/img-20231229184406.png)


SAS 读取第一条观测并使用 DATA 步骤的第一行进行处理，然后是第二行，依此类推，直到 SAS 达到 DATA 步骤的末尾。然后，SAS 将该观测写入输出数据集。这张图说明了逐行循环的第一次执行。一旦 SAS 完成对第一个观测的处理，它会回到 DATA 步骤的顶部并获取第二个观测。当 SAS 完成最后一个观测时，它会自动停止。

除此之外，SAS提供了多种方法调用逐行和逐观测之外的结构。其中包括 `RETAIN` 声明和 `OUTPUT` 声明。

## 2 数据读取

### 2.1 告诉 SAS 原始数据的位置

如果你的数据存储在原始数据文件中，使用 DATA 步骤读取数据具有最大的灵活性。你的原始数据可以是内部数据，即在你的SAS程序中，或者存储在一个单独的文件中。无论哪种方式，你都必须告诉SAS数据的位置。

#### 内部数据

如果你在 SAS 程序中直接输入原始数据，那么这些数据是内嵌在你的程序中的。在数据量较小或者在使用小型测试数据集测试程序时可以这样做。使用`DATALINES`语句来指示内嵌数据。`DATALINES`语句必须是 DATA 步骤中的最后一条语句。在`DATALINES`语句之后的所有行都被视为数据，直到 SAS 遇到一个分号。

```sas
* Read internal data into SAS data set called student;
DATA student;
	INPUT Name $ Gender $ ID;
	DATALINES;
Alex M 1910144
Bill M 1910116
Cindy F 1910233
	;
RUN;
```

#### 外部数据

通常人们将数据存储在外部文件中，将数据与程序分开。这样做可以消除在编辑 SAS 程序时意外更改数据的可能性。使用`INFILE`语句告诉 SAS 外部数据文件的文件名和路径（如果适用）。`INFILE`语句紧跟在 DATA 语句之后，必须位于`INPUT`语句之前。在`INFILE`关键字之后，文件路径和名称应该用引号括起来。

```sas
* Examples from several operating environments;

* Windows;          
INFILE 'c:\MyDir\student.dat'; 
* UNIX;
INFILE '/home/mydir/president.dat'; 
* z/OS;
INFILE 'MYID.STUDENT.DAT'; 
```

假设以下数据存储在名为 student1.dat 的文件中，该文件位于目录 RawData 中：


```txt
Alex M 1910144
Bill M 1910116
Cindy F 1910233
```

以下程序演示了使用`INFILE`语句读取外部数据文件的用法：

```sas
* Read data from external file into SAS data set;
DATA student;
    INFILE 'C://RawData/student.dat';
    INPUT Name $ Gender $ ID;
RUN;
```

### 2.2 读取以空格为间隔符的原始数据

如果原始数据文件中的值都至少被一个空格分隔，那么使用列表输入是一种合适的读取数据的方法。列表输入是将原始数据读入 SAS 的一种简便方式，但有着一些限制。你必须读取记录中的所有数据，不能跳过不需要的值。任何缺失的数据必须用句点表示。如果存在字符数据，它必须是简单的，即没有空格，长度不超过8个字符。如果数据文件包含需要特殊处理的日期或其他值，那么列表输入可能不适用。


`INPUT`语句是 DATA 步骤的一部分，它告诉 SAS 如何读取你的原始数据，只需在`INPUT`关键字之后按照它们在数据文件中出现的顺序列出变量名。通常，变量名不超过32个字符，以字母或下划线开头，只能包含字母、下划线或数字。如果值是字符型（而不是数值型），则在变量名后面加上美元符号（ $ ）。在变量名之间至少留有一个空格。


假设以下数据存储在名为 student2.dat 的文件中，该文件位于目录 RawData 中。这个文件包含了学生在三门课程中的分数。

```txt
Alex 97 98 93
Bill 99 .
92
Cindy 94 . .
Denny . 90 91
```


这个数据文件看起来不太整齐，但它确实符合列表输入的所有要求：字符数据不超过8个字符，没有嵌入的空格，所有值之间至少用一个空格分隔，并且缺失数据用句点表示。请注意，学生 Bill 的数据已经溢出到下一个数据行。这并不会造成报错，因为默认情况下，如果`INPUT`语句中的变量数多于数据行中的值，SAS 会自动转到下一行读取更多数据。

```sas
DATA score;
	INFILE 'C://RawData/student.dat';
	INPUT Name $ Calculus Algebra Stats;
RUN;

* Print the data to make sure the file was correctly read;
PROC PRINT DATA = score;
	TITLE 'SAS Data Set score';
RUN;
```

变量 Name、Calculus、Algebra 和 Stats 在`INPUT`关键字之后按照它们在文件中出现的顺序列出。在 Name 后面的美元符号（ $ ）表示它是一个字符变量；所有其他变量都是数值型的。在读取数据后，使用`PROC PRINT`语句打印数据以确保它们是正确的。在`PROC PRINT`之后的`TITLE`语句告诉 SAS 将引号中的文本放置在每一页输出的顶部。如果你的程序中没有`TITLE`语句，SAS 将在每一页顶部放置“The SAS System”这几个词。


### 2.3 阅读以列形式排列的原始数据

一些原始数据文件中，并非所有的数值或缺失数据之间都有空格（或其他分隔符）, 因此这些文件无法使用列表输入进行读取。但如果每个变量的值始终出现在数据行的相同位置，那么只要所有的值都是字符或标准数值，就可以使用列输入。标准数值数据仅包含数字、小数点、加减号以及用于科学计数法的 E。包含逗号或日期等的数值不属于标准数值。

列输入相对于列表输入具有以下优势：不需要在值之间添加空格；可以将缺失值留空；字符数据可以包含嵌入的空格；可以跳过不需要的变量。

使用列输入时，`INPUT`语句采用以下形式。在`INPUT`关键字之后，列出第一个变量的名称。如果该变量是字符型，留一个空格，然后添加一个\$符号。在\$符号之后，或者如果是数值型，则键入变量名，再留一个空格，然后列出该变量所在的列或列范围。这些列是数据行中字符或数字的位置。


这是一个包含四名学生课程成绩的数据集。它有三个变量：姓名（Name）、部门（Department）和分数（Score）。在数据上方放置了一个显示列号的列标尺，（仅供参考，并非出现在数据集中）。

```txt
----+----1----+----2----+----3
Alex     Biostatistics 97
Bill                   97
Cindy    Epidemiology  95
Denny    Data Science  96
```

```sas
DATA score;
	INFILE 'C://RawData/student3.dat';
	INPUT Name $ 1-9 Department $ 10-23 score 24-25;
RUN;

* Print the data to make sure the file was correctly read;
PROC PRINT DATA = score;
	TITLE 'SAS Data Set score';
RUN;
```


### 2.4 读取非标准格式的原始数据

有时原始数据并不是直接的数值或字符。在SAS中，格式输入（informat）用于告诉计算机如何解释这些类型的数据。

格式输入主要分为三类：字符型、数值型和日期型。这三类格式输入的一般形式如下：

|   Character  |   Numeric   |    Date    |
|:------------:|:-----------:|:----------:|
| $ informatw. | informatw.d | informatw. |

\$ 表示字符格式输入，w 表示总宽度，而 d 表示小数点后的位数（仅适用于数值型输入）。句点是格式输入名称的一个非常重要的部分。如果没有句点，SAS可能会将格式输入视为变量名，默认情况下，变量名不能包含除了下划线以外的任何特殊字符。有两个格式输入没有名称：`$w.`，用于读取标准字符数据，以及`w.d`，用于读取标准数值数据。

这是关于四名学生信息的示例。它有四个变量，分别是姓名（Name）、部门（Department）、出生日期（Birthdate）和课程分数（Score）。每个变量的列是由格式输入的起始点和宽度确定的。SAS 始终从第一列开始；因此，第一个变量 Name 的数据值，其格式输入为`$9.`，位于第1列到第9列。现在，第二个变量的起始点是第10列，SAS 在第10列到第23列读取 Department 的值。出生日期的值从第24列开始，采用日期格式。最后，Score 的值在第35列到第38列。这四列包括数字和小数点。

```txt
Alex     Biostatistics 01-12-2001 97.0
Bill     Statistics    07-01-2002 92.4
Cindy    Epidemiology  12-08-2000 95.2
Denny    Data Science  02-01-2001 96.8
```

```sas
* Create a SAS data set named score;
* Read the file student4.dat using formatted input;
DATA score;
	INFILE 'C://RawData/student4.dat';
	INPUT Name $9. Department $14. Birthdate MMDDYY10. +1 Score 4.1;
RUN;

* Print the data set to make sure the file was correctly read;
PROC PRINT DATA = score;
	TITLE 'SAS Data Set score';
RUN;
```


注意，`+1`表示跳过一列。变量 Birthdate 的输出看起来有些奇怪。例如，日期“01-12-2001”的输出是14987。这是自1960年1月1日以来的天数。这个数字被称为SAS日期值。